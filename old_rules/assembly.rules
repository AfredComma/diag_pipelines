import re
import csv
from snakemake.utils import report

def get_extensions(directory):
    result = []
    extension= {}
    for fname in os.listdir(directory):
        if fname.endswith("fastq.gz") or fname.endswith("fq.gz") or fname.endswith("fastq") or fname.endswith("fq"):
            ext = re.search(r'_R(1|2)(\.|_).*', fname)
            if ext is None:
                ext = re.search(r'f(?:ast)?q(?:\.gz)?', fname)
                samp = re.sub("\.$", "", re.search(r'^([^\.]*)\.*', fname).group(0))
                if samp in extension.keys():
                    if ext.group(0).endswith(".gz"):
                        extension[samp]=ext.group(0)
                else:
                    extension[samp]=ext.group(0)
            else:
                read = re.search(r'R(1|2)', fname).group(0).replace("_", "")
                samp = re.sub(r'_R(1|2)$', '', re.search(r'.*_R(1|2)', fname).group(0))
                extension.setdefault(samp, {})
                if read in extension[samp].keys():
                    if ext.group(0).endswith(".gz"):
                        extension[samp][read]=re.sub("^_", "", ext.group(0))
                else:
                    extension[samp][read]=re.sub("^_", "", ext.group(0))
    return(extension)

extension=get_extensions("reads/raw/")

print(extension)

shell.prefix("set -euo pipefail;") 


rule remove_fasta_part_from_gff:
    input:
        "strains/{sample}/annotation/{sample}.gff"
    output:
        "strains/{sample}/annotation/{sample}_truncated.gff"
    shell:
        "sed '/^##FASTA$/,$d' {input} > {output}"
    
    
rule align_reads_to_assembled_genome:
    conda:
        "env/bwa.yaml"
    input:
        "strains/{sample}/annotation/{sample}.fsa",
        lambda wildcards: "reads/raw/"+wildcards.sample+"_"+extension[wildcards.sample]["R1"],
        lambda wildcards: "reads/raw/"+wildcards.sample+"_"+extension[wildcards.sample]["R2"]
    output:
        "strains/{sample}/mapping/{sample}.bam"
    shell:
        """
        bwa index {input[0]}
        bwa mem {input} | samtools sort -O BAM -o {output} -
        """


rule qualimap_mapping:
    conda:
        "env/qualimap.yaml"
    input:
        "strains/{sample}/mapping/{sample}.bam",
        "strains/{sample}/annotation/{sample}_truncated.gff"
    output:
        "strains/{sample}/mapping/qualimap/qualimapReport.html"
    shell:
        """
        qualimap bamqc -bam {input[0]} -gff {input[1]} -outdir $(dirname {output})
        """



rule mlst:
    conda:
        "env/mlst.yaml"
    input:
        "{sample}/annotation/{sample}.fsa"
    output:
        "{sample}/mlst/mlst.out"
    shell:
        "mlst {input[0]} > {output[0]}"

rule trimmomatic_SE:
    conda:
        "env/trim.yaml"
    input:
        lambda wildcards: "reads/raw/"+wildcards.sample+"."+extension[wildcards.sample]["R1"]
    params:
        minlength = config["minimum_read_length"],
        minqual = config["minimum_quality_base"],
        croplength = config["crop_at_read_beginning"]
    output:
        temp("strains/{sample}/reads/trimmed/single.fastq"),
    log:
        "strains/{sample}/logs/trimmomatic.log"
    shell:
        """
        trimmomatic SE {input[0]} {output[0]} ILLUMINACLIP:${{CONDA_DEFAULT_ENV}}/share/trimmomatic/adapters/TruSeq3-PE.fa:3:25:6 LEADING:{params.minqual} TRAILING:{params.minqual} MINLEN:{params.minlength} HEADCROP:{params.croplength} &> {log}
        """

rule trimmomatic_PE:
    conda:
        "env/trim.yaml"
    input:
        lambda wildcards: "reads/raw/"+wildcards.sample+"_"+extension[wildcards.sample]["R1"],
        lambda wildcards: "reads/raw/"+wildcards.sample+"_"+extension[wildcards.sample]["R2"]
    params:
        minlength = config["minimum_read_length"],
        minqual = config["minimum_quality_base"],
        croplength = config["crop_at_read_beginning"]
    output:
        temp("strains/{sample}/reads/trimmed/R1_paired.fastq"),
        temp("strains/{sample}/reads/trimmed/R1_unpaired.fastq"),
        temp("strains/{sample}/reads/trimmed/R2_paired.fastq"),
        temp("strains/{sample}/reads/trimmed/R2_unpaired.fastq"),
    log:
        "strains/{sample}/logs/trimmomatic.log"
    shell:
        """
        trimmomatic PE {input[0]} {input[1]} {output[0]} {output[1]} {output[2]} {output[3]} ILLUMINACLIP:${{CONDA_DEFAULT_ENV}}/share/trimmomatic/adapters/TruSeq3-PE.fa:3:25:6 LEADING:{params.minqual} TRAILING:{params.minqual} MINLEN:{params.minlength} HEADCROP:{params.croplength} &> {log}
        """        

rule identification:
    conda:
        "env/kraken.yaml"
    params:
        kraken_db=config["kraken_path"]
    input:
        "strains/{sample}/reads/trimmed/R1_paired.fastq",
        "strains/{sample}/reads/trimmed/R2_paired.fastq"
    output:
        "strains/{sample}/logs/kraken.log"
    shell:
        """
        export KRAKEN_DEFAULT_DB="{params.kraken_db}"
        kraken --paired {input[0]} {input[1]} | kraken-report > {output[0]}
        """
        
rule fastqc:
    conda:
        "env/fastqc.yaml"
    input:
        "strains/{sample}/reads/trimmed/R1_paired.fastq",
        "strains/{sample}/reads/trimmed/R2_paired.fastq"
    output:
        "strains/{sample}/reads/trimmed/fastqc/R1_paired_fastqc.zip",
        "strains/{sample}/reads/trimmed/fastqc/R2_paired_fastqc.zip",
        "strains/{sample}/reads/trimmed/fastqc/R1_paired_fastqc.html",
        "strains/{sample}/reads/trimmed/fastqc/R2_paired_fastqc.html"
    shell:
        "fastqc {input} -o $( dirname {output[0]})"
    
rule unzip_fastqc:
    input:
        "strains/{sample}/reads/trimmed/fastqc/R1_paired_fastqc.zip",
        "strains/{sample}/reads/trimmed/fastqc/R2_paired_fastqc.zip"
    output:
        "strains/{sample}/reads/trimmed/fastqc/R1_paired_fastqc/summary.txt",
        "strains/{sample}/reads/trimmed/fastqc/R2_paired_fastqc/summary.txt"
    shell:
        "unzip -D -u {input[0]} -d $( dirname {input[0]}) && unzip -D -u {input[1]} -d $( dirname {input[0]}) && touch {output[0]} &&  touch {output[1]}"

        
rule error_correction_spades_paired:
    conda:
        "env/spades.yaml"
    input:
        "strains/{sample}/reads/trimmed/R1_paired.fastq",
        "strains/{sample}/reads/trimmed/R2_paired.fastq"
    output:
        temp("strains/{sample}/reads/corrected/R1_paired.00.0_0.cor.fastq.gz"),
        temp("strains/{sample}/reads/corrected/R2_paired.00.0_0.cor.fastq.gz"),
        temp("strains/{sample}/reads/corrected/R_unpaired.00.0_0.cor.fastq.gz")
    log:
        "strains/{sample}/logs/spades_read_correction.txt"
    shell:
         """
         spades.py -1 {input[0]} -2 {input[1]} --only-error-correction -o $( dirname $(dirname {output[0]}))
         mv $( dirname $(dirname {output[0]}))/spades.log {log}
         """

rule error_correction_spades_single:
    conda:
        "env/spades.yaml"
    input:
        "strains/{sample}/reads/trimmed/single.fastq",
    output:
        temp("strains/{sample}/reads/corrected/single.00.0_0.cor.fastq.gz"),
    shell:
         "spades.py -s {input[0]} --only-error-correction -o $( dirname $(dirname {output[0]}))"

         
rule check_low_coverage_contigs:
    input:
        "strains/{sample}/assembly/spades/contigs_500bp_low_kmer_coverage.fasta"
    output:
        "strains/{sample}/blast_check/contig_low_kmer_coverage.txt"
    shell:
        "blastn -query {input[0]} -db dub -remote"

        
rule spades_paired:
    conda:
        "env/spades.yaml"
    input:
        "strains/{sample}/reads/corrected/R1_paired.00.0_0.cor.fastq.gz",
        "strains/{sample}/reads/corrected/R2_paired.00.0_0.cor.fastq.gz",
        "strains/{sample}/reads/corrected/R_unpaired.00.0_0.cor.fastq.gz"
    output:
        "strains/{sample}/assembly/spades/contigs.fasta"
    log:
        "strains/{sample}/logs/spades_assembly.txt"
    shell:
        """
        spades.py --only-assembler -1 {input[0]} -2 {input[1]} -s {input[2]} -o $( dirname {output[0]})
        mv $( dirname {output[0]})/spades.log {log}
        mv $( dirname {output[0]})/contigs.fasta $( dirname $(dirname {output[0]}))
        rm -rf $( dirname {output[0]})/*
        mv $( dirname $(dirname {output[0]}))/contigs.fasta $( dirname {output[0]})/
        """

rule spades_single:
    conda:
        "env/spades.yaml"
    input:
        "strains/{sample}/reads/corrected/single.00.0_0.cor.fastq.gz",
    output:
        "strains/{sample}/assembly/spades/contigs.fasta"
    log:
        "strains/{sample}/logs/spades_assembly.txt",
    shell:
        "spades.py --only-assembler -s {input[0]} -o $( dirname {output[0]})"
        
rule quast:
    conda:
        "env/quast.yaml"
    input:
        "strains/{sample}/annotation/{sample}.fsa"
    output:
        "strains/{sample}/assembly/spades/quast/report.txt"
    shell:
        "quast.py {input} -o $( dirname {output})"

rule extract_contigs_500bp:
    input:
        "strains/{sample}/assembly/spades/contigs.fasta"
    output:
        "strains/{sample}/assembly/spades/contigs_500bp.fasta"
    shell:
        "awk '/^>/{{print (NR==1)?$0: \"\\n\" $0;next}} {{printf \"%s\", $0}}END{{print \"\"}}' {input} |  awk \'!/^>/ {{ next }} {{ getline seq }} length(seq) >= 500 {{ print $0 \"\\n\" seq }}\'  > {output}"  


rule rename_contigs:
    input:
        "strains/{sample}/assembly/spades/contigs_500bp_high_kmer_coverage.fasta"
    output:
        "strains/{sample}/assembly/spades/contigs_500bp_high_kmer_coverage_renamed.fasta"
    shell:
        "sed \"s/NODE_\\([0-9]\\+\\)_.*/contig00\\1/\" {input} > {output}"
        
rule prokka:
    conda:
        "env/prokka.yaml"
    input:
        "strains/{sample}/assembly/spades/contigs_500bp_high_kmer_coverage_renamed.fasta"
    output:
        "strains/{sample}/annotation/{sample}.log",
        "strains/{sample}/annotation/{sample}.gff",
        "strains/{sample}/annotation/{sample}.faa",
        "strains/{sample}/annotation/{sample}.fsa",
        "strains/{sample}/annotation/{sample}.txt"
    shell:
        "prokka --outdir $( dirname {output[0]}) --force {input} --prefix {wildcards.sample} --strain {wildcards.sample}"


rule check_coverage_assembly:
    input:
        "strains/{sample}/assembly/spades/contigs_500bp.fasta"
    output:
        "strains/{sample}/assembly/spades/contigs_500bp_low_kmer_coverage.txt",
        "strains/{sample}/assembly/spades/contigs_500bp_high_kmer_coverage.txt"
    shell:
        "grep \">\" {input} | sed \"s/.*cov_//\" | awk '$1 < 5 {{print NR}}' | sed \"s/^/NODE_/\" | sed \"s/$//\" | sed \"s/^>//\" > {output[0]} && grep \">\" {input} | sed \"s/.*cov_//\" | awk '$1 > 5 {{print NR}}' | sed \"s/^/NODE_/\" | sed \"s/$/_/\" | sed \"s/^>//\"  > {output[1]} "

rule abricate_db:
    conda:
        "env/abricate.yaml"
    input:
        "strains/{sample}/annotation/{sample}.fsa"
    output:
        "strains/{sample}/annotation/virulence_resistance/{db}/{sample}.tab"
    shell:
        "abricate --db {wildcards.db} {input} > {output}"


rule abricate_summary:
    conda:
        "env/abricate.yaml"
    input:
        "strains/{sample}/annotation/virulence_resistance/{db}/{sample}.tab"
    output:
        "strains/{sample}/annotation/virulence_resistance/{db}/{sample}_summary.tab"
    shell:
        "abricate --summary {input} > {output}"
        
rule filter_contigs_on_coverage:
    input:
        "strains/{sample}/assembly/spades/contigs_500bp.fasta",
        "strains/{sample}/assembly/spades/contigs_500bp_high_kmer_coverage.txt",
        "strains/{sample}/assembly/spades/contigs_500bp_low_kmer_coverage.txt"
    output:
        "strains/{sample}/assembly/spades/contigs_500bp_high_kmer_coverage.fasta",
        "strains/{sample}/assembly/spades/contigs_500bp_low_kmer_coverage.fasta"
    shell:
        """
        grep -A 1 -f {input[1]} {input[0]} | sed '/^--$/d' > {output[0]}
        if [ -s {input[2]} ]
        then 
            grep -A 1 -f {input[2]} {input[0]} | sed '/^--$/d' > {output[1]}
        else
            touch {output[1]}
        fi
        """

rule convert_quast_images:
    conda:
        "env/imagemagick.yaml"
    input:
        "strains/{sample}/assembly/spades/quast/report.txt"
    output:
        "strains/{sample}/assembly/spades/quast/basic_stats/contigs_500bp_high_kmer_coverage_GC_content_plot.png",
        "strains/{sample}/assembly/spades/quast/basic_stats/contigs_500bp_high_kmer_coverage_coverage_histogram.png"
    shell:
        """
        convert $(dirname {input[0]})/basic_stats/contigs_500bp_high_kmer_coverage_coverage_histogram.pdf $(dirname {input[0]})/basic_stats/contigs_500bp_high_kmer_coverage_coverage_histogram.png
        convert $(dirname {input[0]})/basic_stats/contigs_500bp_high_kmer_coverage_GC_content_plot.pdf $(dirname {input[0]})/basic_stats/contigs_500bp_high_kmer_coverage_GC_content_plot.png
        """
        
    
        

def extract_report(filename, pattern):
    s=''
    with open(filename) as text:
        for i, line in enumerate(text):
            if pattern.search(line) is not None:
                s=s+line+"\n"
    if s.isspace():
        return "    "
    return s

def extract_report_quast(filename):
    with open(filename) as text:
        l = text.readlines()
        return "\n".join(l[-9:-1])

def extract_report_trimmomatic(filename):
    with open(filename) as f:
        lines = f.read().splitlines()
        return [int(s) for s in lines[-2].split() if s.isdigit()]

def format_table(string):
    s = "    "
    for i in string.split("\n"):
        if len(i):
            s = s + "\t".join(i.split("\t")[0:2]) + "\n    " 
    return(s)

def extract_info_quast(l, index):
    return [x for x in l[index].split(" ") if x is not ""][-1]

def extract_info_prokka(filename):
    with open(filename) as f:
        d = {}
        for row in csv.reader(f, delimiter=":"):
            d[row[0]] = row[1]
        return(d)

rule report_assembly:
    conda:
        "env/multiqc.yaml"
    input:
        fastqc1 = "strains/{sample}/reads/trimmed/fastqc/R1_paired_fastqc/summary.txt",
        fastqc2 = "strains/{sample}/reads/trimmed/fastqc/R2_paired_fastqc/summary.txt",
        fastqc1html = "strains/{sample}/reads/trimmed/fastqc/R1_paired_fastqc.html",
        fastqc2html = "strains/{sample}/reads/trimmed/fastqc/R2_paired_fastqc.html",
        low_cov_names = "strains/{sample}/assembly/spades/contigs_500bp_low_kmer_coverage.txt",
        low_cov = "strains/{sample}/assembly/spades/contigs_500bp_low_kmer_coverage.fasta",        
        quast = "strains/{sample}/assembly/spades/quast/report.txt",
        prokka = "strains/{sample}/annotation/{sample}.txt",
        cov_plot = "strains/{sample}/assembly/spades/quast/basic_stats/contigs_500bp_high_kmer_coverage_coverage_histogram.png",
        GC_plot = "strains/{sample}/assembly/spades/quast/basic_stats/contigs_500bp_high_kmer_coverage_GC_content_plot.png",
        kraken = "strains/{sample}/logs/kraken.log"
    output:
        "strains/{sample}/report/multiqc_report.html"
    shell:
        "multiqc $(dirname $(dirname {output})) -o $(dirname {output})"
