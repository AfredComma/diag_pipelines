rule rgi:
    conda:
        "env/rgi.yaml"
    input:
        "strains/{sample}/annotation/{sample}.fsa"
    output:
        "strains/{sample}/resistance/rgi.json"
    shell:
        """
        rgi -t contig -i {input[0]} -o $(dirname {output[0]})/rgi
        """
        

rule rgi_json_to_tsv:
    conda:
        "env/rgi.yaml"
    input:
        "strains/{sample}/resistance/rgi.json"
    output:
        "strains/{sample}/resistance/rgi.tsv"
    shell:
        """
        rgi_jsontab -v ON -i {input[0]} -o {output[0]}
        mv {output[0]}.txt {output[0]}
        """

        
rule abricate:
    conda:
        "env/abricate.yaml"
    input:
        "strains/{sample}/annotation/{sample}.gbk"
    output:
        "strains/{sample}/resistance/abricate.txt"
    log:
        "strains/{sample}/logs/abricate.txt"
    shell:
        """
        abricate --nopath --db resfinder {input[0]} 2> {log} 1> {output[0]} 
        """
        

rule mykrobe_paired:
    conda:
        "env/mykrobe.yaml"
    input:
        "strains/{sample}/reads/trimmed/R1_paired.fastq",
        "strains/{sample}/reads/trimmed/R2_paired.fastq",
    output:
        "strains/{sample}/resistance/mykrobe.json"
    params:
        species = config["spec_for_mykrobe"]
    shell:
        """
        mykrobe predict "{wildcards.sample}" "{params.species}" --seq {input} --min_variant_conf 100  > {output[0]} 
        """

        
rule mykrobe_single:
    conda:
        "env/mykrobe.yaml"
    input:
        "reads/raw/{sample}_single.fastq.gz",
    output:
        "strains/{sample}/resistance/mykrobe.json"
    params:
        species = config["spec_for_mykrobe"]
    shell:
        """
        mykrobe predict "{wildcards.sample}" "{params.species}" --seq {input[0]} --min_variant_conf 100  > {output[0]} 
        """


rule mykrobe_json_to_tsv:
    input:
        "strains/{sample}/resistance/mykrobe.json",
    output:
        "strains/{sample}/resistance/mykrobe.tsv"
    script:
        "scripts/mykrobe_json_to_tsv.py"


rule summary_csv_excel_file:
    params:
        ontology_aro = ontology_file_aro,
        ontology_mo = ontology_file_mo,
        ontology_ro = ontology_file_ro,
        currated_genes = genes
    conda:
        "env/pandas-openpyxl-pronto-xlrd.yaml"
    input:
        expand("strains/{{sample}}/resistance/{software}.tsv", software = config["resistance_prediction_softwares"])
    output:
        "strains/{sample}/resistance/summary.tsv",
        "strains/{sample}/resistance/summary.xlsx",
    script:
        "scripts/summary_csv_xlsx.py"


rule write_congruent_results_fasta:
    conda:
        "env/pandas-openpyxl-pronto-xlrd.yaml"
    input:
        "strains/{sample}/resistance/summary.tsv",
        "strains/{sample}/resistance/rgi.tsv",
    output:
        "strains/{sample}/resistance/sequences.ffn",
        "strains/{sample}/resistance/sequences.faa",
    script:
        "scripts/summary_to_fasta.py"


rule merge_summary_xlsx_files:
    conda:
        "env/pandas-openpyxl-pronto-xlrd.yaml"
    input:
        xlsx=expand("strains/{sample}/resistance/summary.xlsx", sample = read_naming.keys())
    output:
        "resistance_summary.xlsx"
    script:
        "scripts/merge_summary_xlsx.py"

        
        
