rule correct_error_paired_reads_with_spades:
    conda:
        pipeline_path + "envs/spades.yaml"
    threads:
        16
    input:
        R1_trimmed = "samples/{sample}/reads/trimmed/R1_paired.fastq",
        R2_trimmed = "samples/{sample}/reads/trimmed/R2_paired.fastq",
    output:
        temp("samples/{sample}/reads/corrected/R1_paired.00.0_0.cor.fastq.gz"),
        temp("samples/{sample}/reads/corrected/R2_paired.00.0_0.cor.fastq.gz"),
        temp("samples/{sample}/reads/corrected/R_unpaired.00.0_0.cor.fastq.gz")
    log:
        logging_folder + "samples/{sample}/logs/spades_read_correction.txt"
    shell:
         """
         spades.py -1 {input[R1_trimmed]} -2 {input[R2_trimmed]} --only-error-correction -o $( dirname $(dirname {output[0]})) --threads {threads} > {log} 
         """

rule correct_error_single_reads_with_spades:
    conda:
        pipeline_path + "envs/spades.yaml"
    threads:
        16
    input:
        single_trimmed = "samples/{sample}/reads/trimmed/single.fastq",
    output:
        temp("samples/{sample}/reads/corrected/single.00.0_0.cor.fastq.gz")
    log:
        logging_folder + "samples/{sample}/logs/spades_read_correction.txt"
    shell:
         """
         spades.py -s {input[single_trimmed]} --only-error-correction -o $( dirname $(dirname {output[0]})) --threads {threads} > {log} 
         """

        
rule assemble_genome_paired_reads_with_spades:
    conda:
        pipeline_path + "envs/spades.yaml"
    threads:
        16
    shadow:
        "full"
    input:
        R1_corrected = "samples/{sample}/reads/corrected/R1_paired.00.0_0.cor.fastq.gz",
        R2_corrected = "samples/{sample}/reads/corrected/R2_paired.00.0_0.cor.fastq.gz",
        single_corrected = "samples/{sample}/reads/corrected/R_unpaired.00.0_0.cor.fastq.gz",
    output:
        contigs = "samples/{sample}/assembly/spades/contigs.fasta"
    log:
        logging_folder + "samples/{sample}/logs/spades_assembly.txt"
    shell:
        """
        spades.py --only-assembler -1 {input[R1_corrected]} -2 {input[R2_corrected]} -s {input[single_corrected]} -o $( dirname {output[contigs]}) --threads {threads} > {log}
        """

rule assemble_genome_single_reads_with_spades:
    conda:
        pipeline_path + "envs/spades.yaml"
    threads:
        16
    shadow:
        "shallow"
    input:
        single_corrected = "samples/{sample}/reads/corrected/single.00.0_0.cor.fastq.gz",
    output:
        contigs = "samples/{sample}/assembly/spades/contigs.fasta"
    log:
        logging_folder + "samples/{sample}/logs/spades_assembly.txt",
    shell:
        """
        spades.py --only-assembler -s {input[single_corrected]} -o $( dirname {output[contigs]}) --threads {threads} > {log}
        """
        
