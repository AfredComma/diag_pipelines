rule mapping_to_reference_with_bwa:
    conda:
        "env/bwa.yaml"
    input:
        "strains/{sample}/reads/trimmed/R1_paired.fastq",
        "strains/{sample}/reads/trimmed/R2_paired.fastq",        
        "references/{ref}/genome.fasta",
        "references/{ref}/genome.fasta.bwt",
    output:
        "strains/{sample}/mapping/bwa/{ref}/before_validation/{sample}/snps.bam",
        "strains/{sample}/mapping/bwa/{ref}/before_validation/{sample}/snps.bam.bai",
        "strains/{sample}/mapping/bwa/{ref}/before_validation/{sample}/snps.bam.tmp",
    log:
        "strains/{sample}/logs/mapping/{ref}/bwa/log.txt"
    shell:
        """
        if ls strains/{wildcards.sample}/mapping/bwa/{wildcards.ref}/before_validation/{wildcards.sample}/snps.bam.tmp* 1> /dev/null 2>&1; then
             rm strains/{wildcards.sample}/mapping/bwa/{wildcards.ref}/before_validation/{wildcards.sample}/snps.bam.tmp*
        fi 
        (bwa mem -R '@RG\\tID:{wildcards.sample}\\tSM:{wildcards.sample}' {input[2]} {input[0]} {input[1]} -v 1 > {output[2]}) 2> {log}
        samtools view -q 60  -b -u -F 3844 -T {input[2]} {output[2]} | samtools sort -O bam -o {output[0]}
        samtools index {output[0]} 
        """
        
rule remove_duplicates_from_mapping:
    conda:
        "../snp_calling/env/gatk.yaml"
    input:
        "strains/{sample}/mapping/bwa/{ref}/before_validation/{sample}/snps.bam",
    output:
        "strains/{sample}/mapping/bwa/{ref}/before_validation/{sample}/snps_deduplicated.bam",
        "strains/{sample}/mapping/bwa/{ref}/before_validation/{sample}/snps_deduplicated.bai",
    log:
        "strains/{sample}/mapping/bwa/{ref}/metrics_deduplicated.txt",
    shell:
        """
        gatk-launch MarkDuplicates --REMOVE_DUPLICATES true -I {input[0]} -O {output[0]} -M {log}
        gatk-launch BuildBamIndex -I {output[0]}        
        """


rule index_reference:
    conda:
        "env/bwa.yaml"
    input:
        "references/{ref}/genome.fasta"
    output:
        "references/{ref}/genome.fasta.fai",
        "references/{ref}/genome.fasta.bwt",
    shell:
        """
        samtools faidx {input[0]}
        bwa index {input[0]}
        """
    
rule get_ninety_percent_interval_coverage:
    conda:
        "env/r-base.yaml"
    input:
        "strains/{sample}/quality/mapping/bwa/{ref}/qualimap/raw_data_qualimapReport/coverage_histogram.txt"
    output:
        "strains/{sample}/quality/mapping/bwa/{ref}/min_cov.txt",
        "strains/{sample}/quality/mapping/bwa/{ref}/max_cov.txt",
    script:
        "scripts/get_max_min_cov.R"
        
        
