rule mapping_to_reference_with_bwa:
    conda:
        "env/bwa-samtools.yaml"
    input:
        "strains/{sample}/reads/trimmed/R1_paired.fastq",
        "strains/{sample}/reads/trimmed/R2_paired.fastq",        
        "references/{ref}/genome.fasta",
        "references/{ref}/genome.fasta.bwt",
    output:
        temp("strains/{sample}/mapping/bwa/{ref}/{sample}.bam"),
        temp("strains/{sample}/mapping/bwa/{ref}/{sample}.bam.bai"),
    log:
        "strains/{sample}/logs/mapping/{ref}/bwa/log.txt"
    shell:
        """
        if ls {output[0]}.tmp* 1> /dev/null 2>&1
        then
             rm {output[0]}.tmp*
        fi 
        (bwa mem -R '@RG\\tID:{wildcards.sample}\\tSM:{wildcards.sample}' {input[2]} {input[0]} {input[1]} -v 1 | samtools sort -O BAM -o {output[0]}) 2> {log}
        samtools index {output[0]}
        """


rule filter_reads_after_bwa:
    conda:
        "env/bwa-samtools.yaml"
    input:
        "strains/{sample}/mapping/bwa/{ref}/{sample}.bam",
        "strains/{sample}/mapping/bwa/{ref}/{sample}.bam.bai",
        "references/{ref}/genome.fasta",
        "references/{ref}/genome.fasta.bwt",
    output:
        temp("strains/{sample}/mapping/bwa/{ref}/filtered.bam"),
        temp("strains/{sample}/mapping/bwa/{ref}/filtered.bam.bai")
    shell:
        """
        if ls {output[0]}.tmp* 1> /dev/null 2>&1
        then
            rm {output[0]}.tmp*
        fi 
        samtools view -q 60 -b -u -F 3844 -T {input[2]} {input[0]} | samtools sort -O bam -o {output[0]}
        samtools index {output[0]} 
        """
        
rule remove_duplicates_from_mapping:
    conda:
        "../snp_calling/env/gatk.yaml"
    input:
        "strains/{sample}/mapping/bwa/{ref}/filtered.bam",
        "strains/{sample}/mapping/bwa/{ref}/filtered.bam.bai",
    output:
        temp("strains/{sample}/mapping/bwa/{ref}/deduplicated_filtered.bam"),
        temp("strains/{sample}/mapping/bwa/{ref}/deduplicated_filtered.bai"),
    log:
        "strains/{sample}/mapping/bwa/{ref}/metrics_deduplicated.txt",
    shell:
        """
        gatk-launch MarkDuplicates --REMOVE_DUPLICATES true -I {input[0]} -O {output[0]} -M {log}
        gatk-launch BuildBamIndex -I {output[0]}        
        """


rule index_reference:
    conda:
        "env/bwa-samtools.yaml"
    input:
        "references/{ref}/genome.fasta"
    output:
        "references/{ref}/genome.fasta.fai",
        "references/{ref}/genome.fasta.bwt",
    shell:
        """
        samtools faidx {input[0]}
        bwa index {input[0]}
        """
    
rule get_ninety_percent_interval_coverage:
    conda:
        "env/r-base.yaml"
    input:
        "strains/{sample}/quality/mapping/bwa/{ref}/qualimap/raw_data_qualimapReport/coverage_histogram.txt"
    output:
        "strains/{sample}/quality/mapping/bwa/{ref}/min_cov.txt",
        "strains/{sample}/quality/mapping/bwa/{ref}/max_cov.txt",
    script:
        "scripts/get_max_min_cov.R"
        
        
