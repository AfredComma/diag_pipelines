rule mapping_to_reference_with_bwa:
    conda:
        "../../envs/bwa-samtools.yaml"
    input:
        "samples/{sample}/reads/trimmed/R1_paired.fastq",
        "samples/{sample}/reads/trimmed/R2_paired.fastq",        
        ancient("references/{ref}/genome_fasta.fasta"),
        ancient("references/{ref}/genome_fasta.fasta.bwt"),
    output:
        temp("samples/{sample}/mapping/bwa/{ref}/{sample}.bam"),
    log:
        logging_folder+"/samples/{sample}/logs/mapping/{ref}/bwa/log.txt"
    shell:
        """
        if ls {output[0]}.tmp* 1> /dev/null 2>&1
        then
             rm {output[0]}.tmp*
        fi 
        (bwa mem -R '@RG\\tID:{wildcards.sample}\\tSM:{wildcards.sample}' {input[2]} {input[0]} {input[1]} -v 1 | samtools sort -O BAM -o {output[0]}) 2> {log}
        """


rule filter_reads_after_bwa:
    conda:
        "../../envs/bwa-samtools.yaml"
    input:
        "samples/{sample}/mapping/bwa/{ref}/{sample}.bam",
        "samples/{sample}/mapping/bwa/{ref}/{sample}.bam.bai",
        ancient("references/{ref}/genome_fasta.fasta"),
        ancient("references/{ref}/genome_fasta.fasta.bwt"),
    output:
        temp("samples/{sample}/mapping/bwa/{ref}/filtered.bam"),
    shell:
        """
        if ls {output[0]}.tmp* 1> /dev/null 2>&1
        then
            rm {output[0]}.tmp*
        fi 
        samtools view -q 60 -b -u -F 3844 -T {input[2]} {input[0]} | samtools sort -O bam -o {output[0]}
        """
        
rule remove_duplicates_from_mapping:
    conda:
        "../../envs/gatk.yaml"
    input:
        "samples/{sample}/mapping/bwa/{ref}/filtered.bam",
        "samples/{sample}/mapping/bwa/{ref}/filtered.bam.bai",
    output:
        temp("samples/{sample}/mapping/bwa/{ref}/deduplicated_filtered.bam"),
    log:
         logging_folder+"/samples/{sample}/mapping/bwa/{ref}/metrics_deduplicated.txt",
         logging_folder+"/samples/{sample}/logs/mapping/bwa/{ref}/markduplicates.txt"
    shell:
        """
        gatk-launch MarkDuplicates --REMOVE_DUPLICATES true -I {input[0]} -O {output[0]} -M {log[0]} &> {log[1]}
        """


rule get_ninety_percent_interval_coverage:
    conda:
        "../../envs/r-base.yaml"
    input:
        "samples/{sample}/quality/mapping/bwa/{ref}/qualimap/raw_data_qualimapReport/coverage_histogram.txt"
    output:
        "samples/{sample}/quality/mapping/bwa/{ref}/min_cov.txt",
        "samples/{sample}/quality/mapping/bwa/{ref}/max_cov.txt",
    script:
        "scripts/get_max_min_cov.R"
