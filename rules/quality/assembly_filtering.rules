rule copy_raw_assembly_to_reference_folder:
    input:
        contigs="samples/{sample}/assembly/spades/contigs.fasta",
    output:
        fasta=temp("references/{sample}_assembled_genome/genome_fasta.fasta"),
    shell:
        """
        cp {input[contigs]} {output[fasta]}
        """

rule extract_contig_coverage:
    input:
        genome_cov="samples/{sample}/quality/mapping/bwa/{sample}_assembled_genome/qualimap/genome_results.txt"
    output:
        extract="samples/{sample}/quality/contig_coverage.txt"
    shell:
        """
        sed -ne '/Coverage per contig/,$ p'  {input[genome_cov]} | grep contig_ | sed "s/^[ \\t]*//" > {output[extract]}
        """
        
rule quast:
    conda:
        "../../envs/quast.yaml"
    input:
        "samples/{sample}/annotation/{sample}.fsa"
    output:
        "samples/{sample}/quality/quast/report.txt"
    log:
        logging_folder+"/samples/{sample}/logs/assembly/quast.txt",
    shell:
        "quast.py {input[0]} -o $( dirname {output[0]})"



rule filter_contigs_on_coverage:
    params:
        cutoff = config["cov_cutoff"]
    input:
        "samples/{sample}/assembly/spades/contigs_500bp_renamed.fasta",
        "samples/{sample}/quality/contig_coverage.txt"
    output:
        "samples/{sample}/assembly/spades/coverage_filtered/contigs_500bp_high_coverage.fasta",
        "samples/{sample}/assembly/spades/coverage_filtered/contigs_500bp_low_coverage.fasta"
    shell:
        """
        awk '$4 < {params.cutoff}' {input[1]} | cut -f 1 | xargs -I % grep -A 1 "^>%$" {input[0]} > {output[1]}
        awk '$4 > {params.cutoff}' {input[1]} | cut -f 1 | xargs -I % grep -A 1 "^>%$" {input[0]} > {output[0]}
        if [ ! -s {output[0]} ];
        then
            echo "No contigs with coverage higher than {params.cutoff}"
            exit 1
        fi
        """
        

