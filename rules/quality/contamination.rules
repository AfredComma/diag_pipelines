rule blast_low_coverage_contigs:
    conda:
        "env/blast.yaml"
    input:
        "strains/{sample}/assembly/spades/coverage_filtered/contigs_500bp_low_coverage.fasta"
    output:
        "strains/{sample}/contamination/blast_results.tsv",
        "strains/{sample}/contamination/ten_first_contaminated_contigs.fasta"
    shell:
        """
        head -n 20 {input} > {output[1]}
        blastn -max_hsps 1 -max_target_seqs 1 -remote -db nt -query {input[0]} -outfmt "6 qseqid sseqid pident qcovs evalue staxids" | sort -k 6 > {output[0]}
        """

        

rule fetch_taxonomy_from_blast_results:
    conda:
        "env/blast.yaml"
    input:
        "strains/{sample}/contamination/blast_results.tsv"
    output:
        "strains/{sample}/contamination/blast_results_taxid_species.tsv"
    shell:
        """
        cut -f 6 {input}  | sort | uniq | xargs -I % efetch -db taxonomy -id % -format native -mode xml | xtract -pattern TaxaSet -block Taxon -element TaxId ScientificName | sort -k 1 | join -1 6 -2 1 -t $'\\t' --check-order -o 2.2 1.1 1.3 1.6 {input[0]} - > {output[0]}
        """




rule mash_paired_reads:
    conda:
        "env/mash.yaml"
    input:
        "references/mash_sketch.msh",
        "reads/raw/{sample}_R1.fastq.gz",
        "reads/raw/{sample}_R2.fastq.gz"
    output:
        "strains/{sample}/contamination/mash/distances.tsv"
    shell:
        """
        cat {input[1]} {input[2]} | mash screen -w  {input[0]} - > {output[0]}
        """


rule get_taxonomy_from_mash_results:
    conda:
        "env/entrez-direct.yaml"
    input:
        "strains/{sample}/contamination/mash/distances.tsv"
    output:
        "strains/{sample}/contamination/mash/taxonomy.txt"
    shell:
        """
        sort -gr {input[0]}  | cut -f5  | sed "s/\(GCF_[0-9]\+\.[0-9]\+\)_.*/\\1/" | xargs -I % sh -c "esearch -db assembly -query % | efetch -db assembly -format docsum | xtract -pattern DocumentSummary -element SpeciesName" > {output[0]} 
        """


rule get_taxonomy_formated:
    input:
        "strains/{sample}/contamination/mash/distances.tsv"
    output:
        "strains/{sample}/contamination/mash/distances_taxonomy.tsv",
        "strains/{sample}/contamination/mash/distances_taxonomy_no_virus.tsv"        
    shell:
        """
        cat {input[0]}  | cut -f6 | sed "s/\[\.\.\.]//" | sed "s/\[[0-9]\+ seqs] //" | cut -f2- -d' ' | paste {input[0]} - | cut -f1,2,4,7 | sort -gr  > {output[0]}
        cat {input[0]} | cut -f6 | sed "s/\[\.\.\.]//" | sed "s/\[[0-9]\+ seqs] //" | cut -f2- -d' ' | paste {input[0]} - | grep -v "ViralProj" | grep -v "phage" | grep -v "virus" | cut -f1,2,4,7 | sort -gr  > {output[1]}
        """
