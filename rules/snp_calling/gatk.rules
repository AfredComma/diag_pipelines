rule haplotype_caller_vcf:
    conda:
        "env/gatk.yaml"
    input:
        "strains/{sample}/mapping/bwa/{ref}/mapping.bam",
        "references/all_complete_genomes/{ref}_genome.fasta",
        "references/all_complete_genomes/{ref}_genome.dict"
    output:
        "strains/{sample}/snps/gatk/{ref}/full_genome/snps.vcf",
        "strains/{sample}/snps/gatk/{ref}/full_genome/snps.vcf.gz",
        "strains/{sample}/snps/gatk/{ref}/full_genome/snps.vcf.idx",
        "strains/{sample}/snps/gatk/{ref}/full_genome/snps.vcf.gz.tbi",
    shell:
        """
        gatk-launch HaplotypeCaller -ploidy 1 -output {output[0]} --input {input[0]} --reference {input[1]}
        bgzip -c {output[0]} > {output[1]}
        tabix -f -p vcf {output[1]}
        """

rule haplotype_caller_gvcf:
    conda:
        "env/gatk.yaml"
    input:
        "strains/{sample}/mapping/bwa/{ref}/mapping_deduplicated.bam",
        "references/all_complete_genomes/{ref}_genome.fasta",
        "references/all_complete_genomes/{ref}_genome.dict"
    output:
        "strains/{sample}/snps/gatk/{ref}/full_genome/snps.g.vcf",
        "strains/{sample}/snps/gatk/{ref}/full_genome/snps.g.vcf.gz",
        "strains/{sample}/snps/gatk/{ref}/full_genome/snps.g.vcf.idx",
        "strains/{sample}/snps/gatk/{ref}/full_genome/snps.g.vcf.gz.tbi",
    shell:
        """
        gatk-launch HaplotypeCaller -ploidy 1 --output {output[0]} --input {input[0]} --reference {input[1]} -ERC GVCF
        bgzip -c {output[0]} > {output[1]}
        tabix -f -p vcf {output[1]}
        """

rule merge_gvcf_files:
    conda:
        "env/gatk.yaml"
    input:
        ref="references/all_complete_genomes/{ref}_genome.fasta",
        gvcfs=expand("strains/{sample}/snps/gatk/{{ref}}/full_genome/snps.g.vcf.gz", sample=read_naming.keys())
    output:
        "typing/gatk/core_{method}/{ref}/full_genome/genomics_db/vcfheader.vcf"
    shell:
        """
        if [ -d $(dirname {output[0]}) ]; then
            rm -rf $(dirname {output[0]})
        fi
        acc=$(grep ">" {input[ref]} | sed "s/ .*//" | sed "s/>//")
        var=$(echo {input[gvcfs]} | sed "s/ / -V /g")
        gatk-launch GenomicsDBImport --genomicsdb-workspace-path  $(dirname {output[0]}) -V ${{var}} --intervals ${{acc}}
        """

rule extract_gvcf_files:
    conda:
        "env/gatk.yaml"
    input:
        "references/all_complete_genomes/{ref}_genome.fasta",
        "typing/gatk/core_{method}/{ref}/full_genome/genomics_db/vcfheader.vcf"
    output:
        "typing/gatk/core_{method}/{ref}/full_genome/merged.vcf",
        "typing/gatk/core_{method}/{ref}/full_genome/merged.vcf.gz",
        "typing/gatk/core_{method}/{ref}/full_genome/merged.vcf.gz.tbi"
    shell:
        """
        gatk-launch GenotypeGVCFs -R {input[0]} -V gendb://$(dirname {input[1]}) -O {output[0]}
        bgzip -c {output[0]} > {output[1]}
        tabix -f -p vcf {output[1]}
        """
    
rule create_dict_for_reference:
    conda:
        "env/gatk.yaml"
    input:
        "references/all_complete_genomes/{ref}_genome.fasta"
    output:
        "references/all_complete_genomes/{ref}_genome.dict"
    shell:
        """
        samtools faidx {input[0]}
        gatk-launch CreateSequenceDictionary -R {input[0]}
        """


rule extract_only_core_positions:
    conda:
        "../typing/env/bcftools.yaml"
    input:
        "core_genome/{ref}_core_parsnp_sorted_withdot.bed",
        "typing/gatk/core_{method}/{ref}/full_genome/merged.vcf.gz",
    output:
        "typing/gatk/core_{method}/{ref}/before_validation/merged.vcf",
    shell:
        """
        bcftools view -R {input[0]} -Ou {input[1]} | bcftools sort -O v - > {output[0]}
        """
        
        

        

        
    
