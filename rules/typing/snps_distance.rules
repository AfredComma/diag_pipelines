rule snp_distance:
    params:
        samples = list(read_naming.keys())
    conda:
        "env/biopython.yaml"
    input:
        "alignments/{ref}.fa",
        "mapping/snippy/{ref}/core.aln"
    output:
        "alignments/distance_{ref}.txt"
    script:
        "scripts/calculate_number_of_differences.py"

        
rule snp_distance_from_vcf_and_positions_for_close_pairs:
    conda:
        "env/pandas-biopython-pysam.yaml"
    params:
        samples=list(read_naming.keys()),
        dist_thre=int(config["snps_distance_threshold"])
    input:
        gbk="references/{ref}/genome.gbk",
        genotype="typing/core_{method}/{ref}/{validation}/genotype.GT.FORMAT",
        bams=expand("strains/{sample}/mapping/snippy/{{ref}}/{sample}/snps.bam", sample=read_naming.keys())
    output:
        out_csv_dist="typing/core_{method}/{ref}/{validation}/distance.csv",
        out_csv_pos="typing/core_{method}/{ref}/{validation}/positions.csv",
        out_xlsx_dist="typing/core_{method}/{ref}/{validation}/distance.xlsx",
        out_xlsx_pos="typing/core_{method}/{ref}/{validation}/positions.xlsx",
        snps=expand("strains/{sample}/typing/core_{{method}}/{{ref}}/{{validation}}/snps.bed", sample=read_naming.keys())
    script:
        "scripts/snp_distance_from_vcf.py"

        
rule revalidate_snps_with_freebayses:
    conda:
        "env/freebayes.yaml"
    input:
        "strains/{sample}/typing/core_{method}/{ref}/before_validation/snps.bed",
        "strains/{sample}/mapping/snippy/{ref}/before_validation/{sample}/snps.bam",
        "strains/{sample}/mapping/snippy/{ref}/before_validation/{sample}/reference/ref.fa"
    output:
        "strains/{sample}/typing/core_{method}/{ref}/snps_sorted_uniq_minus_one.bed",
        "strains/{sample}/snps/core_{method}/{ref}/after_validation/snps.vcf"
    shell:
        """
        sort -g -k 2 {input[0]} | uniq | awk '{{print $1 " " ($2 - 1) " " $3}}' > {output[0]}
        freebayes -F 0.8 -p 1 -t {output[0]} -f {input[2]} {input[1]} > {output[1]}
        """


rule merge_snps_distance_xlsx:
    conda:
        "env/pandas-biopython-pysam.yaml"
    input:
        expand("typing/core_{{method}}/{ref}/distance.xlsx", ref = config["ref_ids_for_mapping"])
    output:
        "typing/core_{method}/summary.xlsx"
    script:
        "scripts/merge_typing_xlsx.py"


