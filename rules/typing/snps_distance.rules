rule snp_distance:
    params:
        samples = list(read_naming.keys())
    conda:
        "env/biopython.yaml"
    input:
        "alignments/{ref}.fa",
        "mapping/snippy/{ref}/core.aln"
    output:
        "alignments/distance_{ref}.txt"
    script:
        "scripts/calculate_number_of_differences.py"

rule snp_distance_from_vcf:
    conda:
        "env/pandas-biopython-pysam.yaml"
    params:
        samples=list(read_naming.keys()),
        dist_thre=int(config["snps_distance_threshold"])
    input:
        gbk="references/{ref}/genome.gbk",
        genotype="typing/core_{method}/{ref}/genotype.GT.FORMAT",
        bams=expand("strains/{sample}/mapping/snippy/{{ref}}/{sample}/snps.bam", sample=read_naming.keys())
    output:
        "typing/core_{method}/{ref}/distance.csv",
        "typing/core_{method}/{ref}/positions.txt",
        "typing/core_{method}/{ref}/distance.xlsx",
    script:
        "scripts/snp_distance_from_vcf.py"



rule merge_snps_distance_xlsx:
    conda:
        "env/pandas-biopython-pysam.yaml"
    input:
        expand("typing/core_{{method}}/{ref}/distance.xlsx", ref = ref_ids_for_mapping)
    output:
        "typing/core_{method}/summary.xlsx"
    script:
        "scripts/merge_typing_xlsx.py"
