rule extract_snps_core:
    conda:
        "env/vcftools.yaml"
    input:
        "core_genome/{ref}_core_parsnp_sorted.bed",
        "strains/{sample}/mapping/snippy/{ref}/{sample}/snps.vcf.gz"
    output:
        "strains/{sample}/core/{ref}/snps.vcf.gz"
    shell:
        """
        vcftools --gzvcf {input[1]} --bed {input[0]} --recode --stdout | bgzip -c > {output[0]}
        tabix -f -p vcf {output[0]}
        """


rule merge_vcf_files:
    params:
        sample=sorted(read_naming.keys())[0]
    conda:
        "env/vcftools.yaml"
    input:
        expand("strains/{sample}/core/{{ref}}/snps.vcf.gz", sample=sorted(read_naming.keys()))
    output:
        "typing/core_parsnp/{ref}/snps_merged.vcf"
    shell:
        """
        vcf-merge {input} | sed "s/\\tsnps\\t/\\t{params.sample}\\t/" | sed "s/\/core\/{wildcards.ref}\/snps_snps\\t/\\t/g" | sed "s/\/core\/{wildcards.ref}\/snps_snps$//g" > {output[0]}
        """

    

rule extract_genotype_info_no_indels:
    conda:
        "env/vcftools.yaml"
    input:
        "typing/core_{method}/{ref}/snps_merged.vcf"
    output:
        "typing/core_{method}/{ref}/genotype.GT.FORMAT"
    shell:
        """
        vcftools --remove-indels --vcf {input[0]} --extract-FORMAT-info GT --stdout | sed "s/\\t\.\\t/\\t0\\t/g" |  sed "s/\\t\.$/\\t0/" | sed "s/\\t\.\\t/\\t0\\t/g" > {output[0]}
        """
        
