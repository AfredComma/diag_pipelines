rule normalize_and_vt_decompose_from_freebayes:
    conda:
        "../../envs/vt-bcftools.yaml"
    input:
        "typing/freebayes_joint_genotyping/full_genome/{ref}/{mapping_method}/raw/all_samples.vcf.gz",
        "typing/freebayes_joint_genotyping/full_genome/{ref}/{mapping_method}/raw/all_samples.vcf.gz.tbi",
        ref=ancient("references/{ref}/genome_fasta.fasta"),
    output:
        normalized="typing/freebayes_joint_genotyping/full_genome/{ref}/{mapping_method}/all_samples.vcf"
    log:
        logging_folder+"/typing/freebayes_joint_genotyping/full_genome/{ref}/{mapping_method}/vt_normalize.txt"
    shell:
        """
        bcftools norm -f {input[ref]} -m -any {input[0]} | vt decompose_blocksub - | bcftools norm -f {input[ref]} -m +any > {output[normalized]}
        """

rule coverage_filtering_from_freebayes:
    params:
        min_cov=minimum_coverage,
    conda:
        "../../envs/vt-bcftools.yaml"
    input:
        vcf="typing/{snp_caller}/full_genome/{ref}/{mapping_method}/all_samples.vcf",
    output:
        vcf_cov="typing/{snp_caller}/full_genome/{ref}/{mapping_method}/filtered/cov_all_samples.vcf",
    shell:
        """
        bcftools filter -s "lowcov" -S "." -e '(FORMAT/DP)<{params.min_cov}' {input[vcf]} > {output[vcf_cov]}
        """

rule frequency_filtering_per_sample:
    params:
        min_alt=minimum_alternate_frac,
    conda:
        "../../envs/vt-bcftools.yaml",
    input:
        vcf_cov="typing/{snp_caller}/full_genome/{ref}/{mapping_method}/filtered/cov_all_samples.vcf",
    output:
        vcf_cov_freq=temp("samples/{sample}/snps/{snp_caller}/{ref}/{mapping_method}/filtered/freq_cov.vcf"),
    shell:
        """
        bcftools view -s {wildcards.sample} {input[vcf_cov]} | bcftools filter -s "freq" -S "." -e "GT='alt' & MAX(FORMAT/AD[*])/FORMAT/DP < {params.min_alt}" > {output[vcf_cov_freq]}
        """

rule remerge_all_samples_vcf:
    conda:
        "../../envs/vt-bcftools.yaml"
    input:
        vfcs=expand("samples/{sample}/snps/{{snp_caller}}/{{ref}}/{{mapping_method}}/filtered/freq_cov.vcf.gz", sample=list(read_naming.keys())),
        vfcs_tbi=expand("samples/{sample}/snps/{{snp_caller}}/{{ref}}/{{mapping_method}}/filtered/freq_cov.vcf.gz.tbi", sample=list(read_naming.keys())),
    output:
        joint_genotype_vcf="typing/{snp_caller}/full_genome/{ref}/{mapping_method}/filtered/freq_cov_all_samples.vcf",
    shell:
        """
        bcftools merge {input[vfcs]} > {output[joint_genotype_vcf]}
        """
        

rule extract_allele_type_from_joint_genotyping_gatk:
    conda:
        "../../envs/gatk.yaml"
    input:
        joint_genotype_vcf="typing/gatk_gvcfs/full_genome/{ref}/{mapping_method}/filtered/freq_cov_all_samples.vcf.gz",
        joint_genotype_vcf_tbi="typing/gatk_gvcfs/full_genome/{ref}/{mapping_method}/filtered/freq_cov_all_samples.vcf.gz.tbi",
    output:
        vcf_mutation_type="typing/gatk_gvcfs/full_genome/{ref}/{mapping_method}/filtered/all_samples_{type}.vcf",
    shell:
        """
        type=$(echo {wildcards.type}  | tr '[:lower:]' '[:upper:]')
        gatk-launch SelectVariants --output {output[vcf_mutation_type]} --variant {input[joint_genotype_vcf]} -select-type ${{type}}
        """

rule extract_allele_type_from_joint_genotyping_freebayes:
    conda:
        "../../envs/vt-bcftools.yaml"
    input:
        joint_genotype_vcf="typing/freebayes_joint_genotyping/full_genome/{ref}/{mapping_method}/filtered/freq_cov_all_samples.vcf.gz",
        joint_genotype_vcf_tbi="typing/freebayes_joint_genotyping/full_genome/{ref}/{mapping_method}/filtered/freq_cov_all_samples.vcf.gz.tbi",
    output:
        vcf_mutation_type="typing/freebayes_joint_genotyping/full_genome/{ref}/{mapping_method}/filtered/all_samples_{type}.vcf",
    log:
        logging_folder+"/typing/freebayes_joint_genotyping/full_genome/{ref}/{mapping_method}/filtered/all_samples_{type}.txt"
    shell:
        """
        bcftools view -i 'type="{wildcards.type}"' {input[joint_genotype_vcf]} > {output[vcf_mutation_type]}
        """



